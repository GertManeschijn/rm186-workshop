// Importeer RM definities en libraries
#include "RM1xx-defs.h"
#include "sleep.sblib"
#include "bme280.sblib"
#include "sds011.sblib"


// Definieer de zend interval en eerste zend interval
#define INTERVAL ( 1 * 60 * 1000 ) // 1 minuut


// #define INTERVAL ( 5 * 60 * 1000 ) // 5 minuten

#define FIRSTINT ( 10 * 1000 ) // 10 seconden

// Definieer de benodigde variabelen
DIM data$
DIM rc
DIM joined

// Join is geslaagd
// Herstart de timer met de interval voor het eerste packet
function LoraJoined() As Integer
	// Schedule first transmission
	PRINT "Joined\n"
	TimerCancel(1)
	joined = 1
    TimerStart(1, FIRSTINT, 0)
endfunc 1

// Verzenden van een packet is afgerond, inclusief wachten op retour data
function LoraTxComplete() As Integer
endfunc 1

// Verzenden van data is mislukt met een timeout
function LoraTxTimeout() As Integer
endfunc 1

// Zend data
// Cancel een eventueel lopende timer,
// controleer of we al gejoined zijn en zend data als dat het geval is.
// Herstart join als we nog niet gejoined zijn.
// Start de timer opnieuw voor de volgende poging.
FUNCTION SendData() AS Integer
    DIM pressure
	DIM temp
	DIM humidity
	DIM iChr
	DIM iResult
	DIM sds$
	DIM pm10
	DIM pm25
	Dim CayenneLPP$

	// PRINT "SendData, joined = ";joined;"\n"
	TimerCancel(1)
	IF joined == 1 THEN
		// Cayenne LPP Low Power Payload format
		// Channel 1, temperature 2 bytes signed index 2,3
		// channel 2, Humidity 1 byte unsigned index 6
		// Channel 3, Pressure 2 bytes unsigned index 9, 10
		// Channel 4, Analog input SDS011 PM25 2 bytes unsigned index 13, 14
		// Channel 5, Analog input SDS011 PM10 2 bytes  index 17, 18
		CayenneLPP$ = "\01\67\00\00\02\68\00\03\73\00\00\04\02\00\00\05\02\00\00"

		BME280_Read_Raw()
		temp = BME280_Read_Temp()
		pressure = BME280_Read_Pressure()
		humidity = BME280_Read_Humidity()
		
		SDS011Start()
		Sleep(30000)
		sds$=SDS011ReadData(1,1)
		SDS011Sleep()
		
		iChr = STRGETCHR(sds$,0)
		IF iChr == 0xC0 THEN
			pm25 = SDS011GetPM25(sds$)
			pm10 = SDS011GetPM10(sds$)
		ENDIF

		iResult = StrSetChr(CayenneLPP$,temp/256,2)
		iResult = StrSetChr(CayenneLPP$,temp%256,3)
		
		iResult = StrSetChr(CayenneLPP$,humidity/256,6)
		iResult = StrSetChr(CayenneLPP$,pressure/256,9)
		iResult = StrSetChr(CayenneLPP$,pressure%256,10)

		iResult = StrSetChr(CayenneLPP$,pm25/256,13)
		iResult = StrSetChr(CayenneLPP$,pm25%256,14)

		iResult = StrSetChr(CayenneLPP$,pm10/256,17)
		iResult = StrSetChr(CayenneLPP$,pm10%256,18)
		

		// PRINT "Sending data\n"
		rc = LORAMACTxData(1,CayenneLPP$,0)
		// PRINT "Send rc:";rc;"\n"
	else
		// PRINT "Attemp join again\n"
		rc = LORAMACJoin(1)
	endif
	TimerStart(1, INTERVAL, 0)
endfunc 1

// Handel fout condities af. (Negeer ze gewoon)
SUB HandleOnError()
	PRINT "\nERROR Handler called!\n"
ENDSUB

// Handel ontvangen data af (negeer voor nu)
FUNCTION LoraRxData() as INTEGER
	PRINT "Data received"
ENDFUNC 1

// main code, zet alle event handlers.
ONEVENT EVTMR1 				CALL SendData
ONEVENT EVLORAMACJOINED 	CALL LoraJoined
ONEVENT EVLORAMACTXCOMPLETE CALL LoraTxComplete
ONEVENT EVLORAMACTXTIMEOUT 	CALL LoraTxTimeout
ONEVENT EVLORAMACRXDATA 	CALL LoraRxData

ONERROR NEXT HandleOnError
ONFATALERROR NEXT HandleOnError

// Maak de te zenden data leeg.
data$ = ""

// Initialiseer BME sensor
rc = BME280_Open(BME280_OVERSAMPLE1,BME280_OVERSAMPLE1,BME280_OVERSAMPLE1,0)
if (rc == 0) then
    print "BME open\nRead callibration\n"
    BME280_ReadCallibarationVals()
	Sleep(2000)

    PRINT "\nstarting LoRaWAN operation\n"

    // Start LoRaWAN, Start timer en start Join procedure
    joined = 0
    PRINT "Start joining\n"
    rc = LORAMACJoin(1)

	print "Be sure to disconnect tx/rx from Laird because of SDS011. Yoh have 20s :-)\n"
	Sleep(20000)
	
	// We want the uart clean before we open it for the SDS011
	UartClose()
	
	// Set SDS011 to sleep
	rc = UartOpen(9600,0,0,"CN81N")
	UartFlush(3)
	Sleep(100)
	rc=UartWrite(sleep$)
	Sleep(2000)
	UartFlush(3)
	UartClose()

    TimerStart(1, INTERVAL, 0)
	
	// Loop forever
	do
		WAITEVENT
	until 0	
	
else
    print "BME280 failed "
    print "\nFailed to open I2C interface with error code "
    print rc
    print "\n Leaving application, nothing to do!\n"
endif










